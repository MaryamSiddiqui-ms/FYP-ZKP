import "./sha256/512bitPadded" as hash;
import "./256bitsDirectionHelper" as multiplex;

// leave the root out of the struct as all the variables 
// in the struct are all private and the root is public
struct MerkleTreeProofStruct<DEPTH> {
  u32[8] leaf;
  bool[DEPTH] directionSelector; 
  u32[DEPTH][8] path;
}

// directionSelector => true if current digest is on the rhs of the hash
def select(bool condition, u32[8] left, u32[8] right) -> (u32[8], u32[8]) {
  return (condition ? right : left, condition ? left : right);
}

// Merkle-Tree inclusion proof for tree depth <DEPTH> using sha256
def merkleTreeProof<DEPTH>(u32[8] root, MerkleTreeProofStruct<DEPTH> proof) -> bool {
    // Start from the leaf
    u32[8] mut digest = proof.leaf;

  // Loop up the tree
  for u32 i in 0..DEPTH {
    (u32[8], u32[8]) s = select(proof.directionSelector[i], digest, proof.path[i]);
    digest = hash(s.0, s.1);
  }

    return digest == root;
}

const u32 TREE_DEPTH = 3;

def main(u32[8] treeRoot ,private MerkleTreeProofStruct<TREE_DEPTH> proof) {
    assert(merkleTreeProof(treeRoot, proof));
}